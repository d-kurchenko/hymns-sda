apply plugin: 'com.android.application'

def getVersionNameFromPackageJson() {
    def file = new File(rootDir, "../package.json")
    def jsonSlurper = new groovy.json.JsonSlurper()
    def packageJson = jsonSlurper.parseText(file.text)
    return packageJson.version
}

def getVersionCode(versionName) {
    def versionParts = versionName.split("\\.")
    if (versionParts.size() != 3) {
        throw new IllegalArgumentException("Version name must be in the format 'X.Y.Z'")
    }
    def major = Integer.parseInt(versionParts[0]) * 10000
    def minor = Integer.parseInt(versionParts[1]) * 100
    def patch = Integer.parseInt(versionParts[2])
    return major + minor + patch
}

android {
    namespace "com.hymns.sda"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.hymns.sda"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode getVersionCode(getVersionNameFromPackageJson())
        versionName getVersionNameFromPackageJson()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == 'release') {
        variant.outputs.each { output ->
            def versionName = variant.versionName
            def newFileName = "hymns-sda-v${versionName}.apk"
            output.outputFileName = newFileName
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
